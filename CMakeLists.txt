# SPDX-FileCopyrightText: none
# SPDX-License-Identifier: BSD-3-Clause

cmake_minimum_required(VERSION 3.16 FATAL_ERROR)
# Do NOT add quote
set(KDEPIM_DEV_VERSION alpha)

# add an extra space
if(DEFINED KDEPIM_DEV_VERSION)
    set(KDEPIM_DEV_VERSION " ${KDEPIM_DEV_VERSION}")
endif()

set(PIM_VERSION "6.6.40")
set(RELEASE_SERVICE_VERSION "26.03.40")

# Update it for each release
set(KORGANIZER_RELEASE_VERSION_DATE "26.03.10") # yy.MM.dd

set(KDEPIM_VERSION "${PIM_VERSION}${KDEPIM_DEV_VERSION} (${RELEASE_SERVICE_VERSION})")

project(korganizer VERSION ${PIM_VERSION})

set(KF_MIN_VERSION "6.20.0")
set(QT_REQUIRED_VERSION "6.9.0")
find_package(ECM ${KF_MIN_VERSION} CONFIG REQUIRED)

set(CMAKE_MODULE_PATH ${ECM_MODULE_PATH})

include(ECMInstallIcons)
include(ECMSetupVersion)
include(ECMAddTests)

include(GenerateExportHeader)
include(ECMGenerateHeaders)

file(
    GLOB_RECURSE ALL_CLANG_FORMAT_SOURCE_FILES
    src/*.cpp
    src/*.h
    plugins/*.cpp
    plugins/*.h
)

set(PRE_COMMIT_INSTALLED FALSE)
if(${CMAKE_SOURCE_DIR} STREQUAL ${PROJECT_SOURCE_DIR})
    if(EXISTS "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit")
        file(READ "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit" FILE_CONTENTS)
        string(FIND "${FILE_CONTENTS}" "File generated by pre-commit" INDEX)
        if(${INDEX} GREATER_EQUAL 0)
            set(PRE_COMMIT_INSTALLED TRUE)
        endif()
    endif()
endif()
if(NOT ${PRE_COMMIT_INSTALLED})
    find_program(PRE_COMMIT_EXECUTABLE pre-commit)
    if(PRE_COMMIT_EXECUTABLE)
        message(
            WARNING
            "This module has pre-commit support. pre-commit binary was found. Please execute pre-commit install -f"
        )
    endif()
    include(KDEGitCommitHooks)
    include(KDEClangFormat)
    kde_clang_format(${ALL_CLANG_FORMAT_SOURCE_FILES})
endif()
include(CheckFunctionExists)

include(KDEInstallDirs)
include(KDECMakeSettings)
include(KDECompilerSettings NO_POLICY_SCOPE)
include(ECMAddAppIcon)
include(ECMQtDeclareLoggingCategory)
include(ECMDeprecationSettings)
include(ECMFeatureSummary)

include(ECMCheckOutboundLicense)
ecm_check_outbound_license(LICENSES GPL-2.0-only GPL-3.0-only FILES
                           ${ALL_CLANG_FORMAT_SOURCE_FILES}
)

# Require a C++20 capable compiler
set(CMAKE_CXX_SCAN_FOR_MODULES OFF)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED TRUE)

set(AKONADI_CONTACT_VERSION "6.6.40")
set(KMAILTRANSPORT_LIB_VERSION "6.6.40")
set(CALENDARUTILS_LIB_VERSION "6.6.40")
set(AKONADICALENDAR_LIB_VERSION "6.6.40")
set(KONTACTINTERFACE_LIB_VERSION "6.6.40")
set(KMIME_LIB_VERSION "6.6.40")
set(AKONADI_VERSION "6.6.40")

set(KDEPIM_LIB_VERSION "${PIM_VERSION}")
set(KDEPIM_LIB_SOVERSION "6")

find_package(
    Qt6
    ${QT_REQUIRED_VERSION}
    CONFIG
    REQUIRED
        DBus
        Gui
        Widgets
        Test
        UiTools
)

set(PIMCOMMON_LIB_VERSION "6.6.40")
set(LIBKDEPIM_LIB_VERSION "6.6.40")
set(LIBINCIDENCEEDITOR_LIB_VERSION "6.6.40")
set(CALENDARSUPPORT_LIB_VERSION "6.6.40")
set(EVENTVIEW_LIB_VERSION "6.6.40")

# Find KF6 package
find_package(KF6I18n ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6Config ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6CoreAddons ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6Crash ${KF_MIN_VERSION} REQUIRED)
find_package(KF6IconThemes ${KF_MIN_VERSION} REQUIRED)
find_package(KF6ItemViews ${KF_MIN_VERSION} REQUIRED)
find_package(KF6JobWidgets ${KF_MIN_VERSION} REQUIRED)
find_package(KF6KCMUtils ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6Parts ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6WidgetsAddons ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6WindowSystem ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6XmlGui ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6Holidays ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6UserFeedback ${KF_MIN_VERSION} CONFIG)

set_package_properties(
    KF6UserFeedback
    PROPERTIES
        DESCRIPTION
            "User Feedback lib"
        TYPE
            OPTIONAL
        PURPOSE
            "Allow to send Telemetry Information (optional)."
)

if(TARGET KF6::UserFeedbackWidgets)
    set(KORGANIZER_WITH_KUSERFEEDBACK true)
endif()

find_package(KF6DocTools ${KF_MIN_VERSION})
set_package_properties(
    KF6DocTools
    PROPERTIES
        DESCRIPTION
            "Tools to generate documentation"
        TYPE
            OPTIONAL
)

# Find KdepimLibs Package
find_package(KPim6Akonadi ${AKONADI_VERSION} CONFIG REQUIRED)
find_package(KF6Contacts ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6CalendarCore ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KPim6AkonadiContactWidgets ${AKONADI_CONTACT_VERSION} CONFIG REQUIRED)
find_package(KPim6MailTransport ${KMAILTRANSPORT_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6CalendarUtils ${CALENDARUTILS_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6LdapWidgets ${KLDAP_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6AkonadiCalendar ${AKONADICALENDAR_LIB_VERSION} CONFIG REQUIRED)

find_package(KPim6KontactInterface ${KONTACTINTERFACE_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6Mime ${KMIME_LIB_VERSION} CONFIG REQUIRED)

find_package(KPim6PimCommonAkonadi ${PIMCOMMON_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6Libkdepim ${LIBKDEPIM_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6IncidenceEditor ${LIBINCIDENCEEDITOR_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6CalendarSupport ${CALENDARSUPPORT_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6EventViews ${EVENTVIEW_LIB_VERSION} CONFIG REQUIRED)
find_package(KPim6PimCommonActivities ${PIMCOMMON_LIB_VERSION} CONFIG)

if(TARGET KPim6::PimCommonActivities)
    option(OPTION_USE_PLASMA_ACTIVITIES "Activate plasma activities (experimental)" ON)
    if(OPTION_USE_PLASMA_ACTIVITIES)
        if(NOT WIN32 AND NOT APPLE)
            set(OPTION_USE_PLASMA_ACTIVITIES true)
        else()
            set(OPTION_USE_PLASMA_ACTIVITIES false)
        endif()
    endif()
    if(OPTION_USE_PLASMA_ACTIVITIES)
        if(NOT WIN32 AND NOT APPLE)
            find_package(PlasmaActivities "6.4.0")
            set_package_properties(
                PlasmaActivities
                PROPERTIES
                    DESCRIPTION
                        "Allow to compile with plasma activities"
                    TYPE
                        OPTIONAL
            )
        endif()

        if(TARGET Plasma::Activities)
            set(HAVE_ACTIVITY_SUPPORT TRUE)
        endif()
    endif()
endif()

if(NOT APPLE)
    find_package(X11)
endif()

set(KTEXTADDONS_MIN_VERSION "1.8.82")

find_package(KF6TextAddonsWidgets ${KTEXTADDONS_MIN_VERSION} CONFIG)
set_package_properties(
    KF6TextAddonsWidgets
    PROPERTIES
        DESCRIPTION
            "Add text addons widget"
        TYPE
            REQUIRED
)

set(KDEPIM_HAVE_X11 ${X11_FOUND})
configure_file(config-korganizer.h.in ${CMAKE_CURRENT_BINARY_DIR}/config-korganizer.h)
include_directories(
    ${korganizer_SOURCE_DIR}
    ${korganizer_BINARY_DIR}
)
configure_file(korganizer-version.h.in ${CMAKE_CURRENT_BINARY_DIR}/korganizer-version.h @ONLY)

add_definitions(-DQT_NO_CONTEXTLESS_CONNECT)
ecm_set_disabled_deprecation_versions(QT 6.10.0 KF 6.21.0)

option(USE_UNITY_CMAKE_SUPPORT "Use UNITY cmake support (speedup compile time)" OFF)

set(COMPILE_WITH_UNITY_CMAKE_SUPPORT OFF)
if(USE_UNITY_CMAKE_SUPPORT)
    set(COMPILE_WITH_UNITY_CMAKE_SUPPORT ON)
endif()

add_subdirectory(src)
add_subdirectory(plugins)

ecm_qt_install_logging_categories(
  EXPORT KORGANIZER FILE korganizer.categories DESTINATION
  ${KDE_INSTALL_LOGGINGCATEGORIESDIR}
)

if(DEFINED kde_configure_git_pre_commit_hook)
    kde_configure_git_pre_commit_hook(CHECKS CLANG_FORMAT)
endif()
ki18n_install(po)

if(KF6DocTools_FOUND)
    kdoctools_install(po)
    add_subdirectory(doc)
endif()

ecm_feature_summary(WHAT ALL FATAL_ON_MISSING_REQUIRED_PACKAGES)
